phase title{
    // じゃんけんカウントを0にする
    common.count = 0;
    print("じゃんけん");
    input();
}
phase action{
    //IDが0の人がカウントを１だけ加える
    if (my.id == 0){
        common.count ++;
    }

    //全員じゃんけんの手を決める
    print("#{common.count}回目です。")
    selector = new Selector('hand');
    selector.app_option('ぐー', 'g');
    selector.app_option('ちょき', 'c');
    selector.app_option('ぱー', 'p');
    result = input();
    my.hand = result['hand'];
}
phase result{
    //IDが0の人が審判役をやる
    if (my.id == 0){
        //グーを出したユーザーが存在するか調べる
        g_exists = members.where(hand == 'g').exists?;
        //チョキを出したユーザーが存在するか調べる
        c_exists = members.where(hand == 'c').exists?;
        //パーを出したユーザーが存在するか調べる
        p_exists = members.where(hand == 'p').exists?;

        //買った手と負けた手を設定する(引き分けの場合はnull)
        common.winner_hand = null;
        common.loser_hand = null;
        if ((g_exists == false)&&(c_exists == true)&&(p_exists == true)){
            common.winner_hand = 'c';
            common.loser_hand = 'p';
        }else if ((g_exists == true)&&(c_exists == false)&&(p_exists == true)){
            common.winner_hand = 'p';
            common.loser_hand = 'g';
        }else if ((g_exists == true)&&(c_exists == true)&&(p_exists == false)){
            common.winner_hand = 'g';
            common.loser_hand = 'c';
        }

        //計算が終了したら、全員にそう伝える
        a_notification("CALC_END");
    }else{
        //計算の終了を待つ
        wait_notification("CALC_END");
    }
    if (my.hand == common.winner_hand){
        print("あなたは勝ちです");
    }else if (my.hand == common.loser_hand){
        print("あなたは負けです");
    }else{
        print("あなたは引き分けです");
    }
    input();
}

}
